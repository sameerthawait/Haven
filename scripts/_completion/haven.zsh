#compdef haven haven-backup haven-calendar haven-contacts haven-cookbook haven-docs haven-gallery haven-mail haven-mcp haven-memory haven-notes haven-personal haven-preset haven-research haven-sessions haven-signature haven-skills haven-tasks haven-theme haven-webhook
# Zsh tab-completion for the haven umbrella + sub-CLIs.
#
# Drop in any directory on $fpath, e.g.:
#     fpath=(/path/to/haven-ui/scripts/_completion $fpath)
#     autoload -U compinit; compinit
#
# Then `haven <tab>` completes subcommands; `haven mail <tab>`
# completes mail subcommands; `haven-mail <tab>` works the same.

_haven_scripts_dir() {
    local self="${(%):-%x}"
    while [[ -L "$self" ]]; do self="$(readlink "$self")"; done
    cd "${self:h}/.." && pwd
}

typeset -gA _haven_subs

_haven_refresh() {
    _haven_subs=()
    local dir="$(_haven_scripts_dir)"
    local py="$dir/../venv/bin/python"
    [[ -x "$py" ]] || py="$(command -v python3)"
    local f sub help_out commands
    for f in "$dir"/haven-*; do
        [[ -x "$f" ]] || continue
        case "$f" in
            *.bak|*.pyc|*.pre-*) continue ;;
        esac
        sub="${${f:t}#haven-}"
        help_out=$("$py" "$f" --help 2>/dev/null) || continue
        commands=$(echo "$help_out" | grep -oE '\{[a-z0-9_,-]+\}' | head -1 \
            | tr -d '{}' | tr ',' ' ')
        _haven_subs[$sub]="$commands"
    done
}

_haven() {
    [[ ${#_haven_subs} -eq 0 ]] && _haven_refresh

    local cmd="${words[1]}"

    if [[ "$cmd" == "haven" ]]; then
        if (( CURRENT == 2 )); then
            local -a subs=(${(k)_haven_subs} help)
            _describe 'subcommand' subs
            return
        fi
        local sub="${words[2]}"
        if [[ "$sub" == "help" ]] && (( CURRENT == 3 )); then
            local -a subs=(${(k)_haven_subs})
            _describe 'subcommand' subs
            return
        fi
        if (( CURRENT == 3 )); then
            local -a sc=(${(s/ /)_haven_subs[$sub]})
            _describe 'command' sc
            return
        fi
        return
    fi

    # haven-foo <tab>
    local sub="${cmd#haven-}"
    if (( CURRENT == 2 )); then
        local -a sc=(${(s/ /)_haven_subs[$sub]})
        _describe 'command' sc
        return
    fi
}

_haven "$@"
